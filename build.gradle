buildscript {
    ext {
        lombokVersion = '1.18.0'
        myRepo = {
            url 'https://dl.bintray.com/reeson2003/maven/'
        }
    }
    repositories {
        jcenter()
        maven myRepo
    }
}

plugins {
    id 'java-library'
    id 'idea'
    id 'com.bmuschko.clover' version '2.2.1'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply from: "$project.rootDir/gradle/code-coverage.gradle"
apply from: "$project.rootDir/gradle/integration-test.gradle"
apply from: "$project.rootDir/gradle/artifacts.gradle"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'

group = 'ru.reeson2003'
version ='1.0.0'

dependencies {
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.projectlombok:lombok:${lombokVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.12'

    clover 'org.openclover:clover:4.2.0'
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenPublication']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Reeson2003/machinery.git'
        version {
            name = project.version
            desc = project.description
            released  = new Date()
        }
    }
}
